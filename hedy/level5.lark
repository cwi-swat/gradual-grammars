%import common.NUMBER -> NUMBER
%import common.SIGNED_INT -> INT
_SPACE: " "+

_COMMA: _SPACE? ("," | "،") _SPACE?

start: program

echo: "echo" (_SPACE text)?

error_invalid_space: _SPACE text?

comment: "#" /([^\n]+)/

_EOL: "\r"? "\n"

NAME: ID_START ID_CONTINUE*

ID_START: /[\p{Lu}\p{Ll}\p{Lt}\p{Lm}\p{Lo}\p{Nl}_]+/

ID_CONTINUE: ID_START
  | /[\p{Mn}\p{Mc}\p{Nd}\p{Pc}·]+/

_END_BLOCK: "end-block"

error_ask_dep_2: "ask" (_SPACE error_text_dep_2)?

error_echo_dep_2: "echo" (_SPACE error_text_dep_2)?

error_text_dep_2: /([^\n]+)/ -> text

sleep: "sleep" (_SPACE INT)?

text_ask: /([^\r\n!?.]+)/ -> text

punctuation: PUNCTUATION -> punctuation

PUNCTUATION: "!"
  | "?"
  | "."

var: NAME -> var

text_list: /([^\r\n,،]+)/ -> text

random: "random"

list_access: var _SPACE "at" _SPACE (INT | random) -> list_access

turtle: "forward" (_SPACE (INT | list_access | textwithoutspaces))? -> forward
  | "turn" (_SPACE (list_access | textwithoutspaces))? -> turn

some_spaces: /([^\r\n!?. ]+)/ (_SPACE /([^\r\n!?. ]+)/)* -> text_with_spaces

add: "add" _SPACE some_spaces _SPACE "to" _SPACE var

remove: "remove" _SPACE some_spaces _SPACE "from" _SPACE var

var_access: NAME

print: "print" (_SPACE _print_argument)? -> print

ask: var _SPACE "is" _SPACE "ask" (_SPACE _print_argument)?

error_ask_no_quotes: var _SPACE "is" _SPACE "ask" _SPACE text -> error_print_nq

_print_argument: (_SPACE | list_access | quoted_text | var_access)*

text: /([^\n,]+)/ -> text

quoted_text_no_escape: /'([^']*)'/ -> text

quoted_text: /'((?:[^\\']|\\.)*)'/ -> text

program: _EOL* (command | error_invalid) (_SPACE)* (_EOL+ (command | error_invalid) (_SPACE)*)* _EOL*

command: print
  | ask
  | turtle
  | assign_list
  | add
  | remove
  | sleep
  | error_print_no_quotes
  | comment
  | ifelse
  | ifs
  | list_access_var
  | assign

list_access_var: var _SPACE "is" _SPACE var _SPACE "at" _SPACE (INT | random)

assign_list: var _SPACE "is" _SPACE textwithspaces (_COMMA textwithspaces)+

assign: var _SPACE "is" _SPACE textwithspaces

error_invalid: textwithoutspaces (_SPACE textwithspaces)?

error_print_no_quotes: "print" _SPACE (textwithoutspaces | list_access | var) (_SPACE (textwithoutspaces | list_access | var))* -> error_print_nq

ifelse: "if" _SPACE condition (_SPACE | _EOL*) command (_SPACE | _EOL*) "else" (_SPACE | _EOL*) command

ifs: "if" _SPACE condition (_SPACE | _EOL*) command

condition: equality_check
  | in_list_check

equality_check: textwithoutspaces _SPACE "is" _SPACE textwithoutspaces (_SPACE textwithoutspaces)*

in_list_check: textwithoutspaces _SPACE "in" _SPACE var

nospace: /[^\n, ]/

textwithspaces: /(?:[^\n،, ]| (?!else))+/ -> text

textwithoutspaces: /(?:[^\n،, ei]|e(?!lse)|i(?!f))+/ -> text

%import common.NUMBER -> NUMBER
%import common.SIGNED_INT -> INT
_SPACE: " "+

start: program

comment: "#" /([^\n]+)/

NAME: ID_START ID_CONTINUE*

ID_START: /[\p{Lu}\p{Ll}\p{Lt}\p{Lm}\p{Lo}\p{Nl}_]+/

ID_CONTINUE: ID_START
  | /[\p{Mn}\p{Mc}\p{Nd}\p{Pc}·]+/

_END_BLOCK: "end-block"

WS: _SPACE?

color: "black"
  | "blue"
  | "brown"
  | "gray"
  | "green"
  | "orange"
  | "pink"
  | "purple"
  | "red"
  | "white"
  | "yellow"

list_access: NAME "at" (INT | "random")

text_ask: /([^\n!?.]+)/ -> text

text_list: /([^\n,،，]+)/ -> text

some_spaces: /([^\r\n!?. ]+)/ (_SPACE /([^\r\n!?. ]+)/)* -> text

text: /([^\n،,，]+)/ -> text

quoted_text: (/'((?:[^\\']|\\.)*)'/ | /"((?:[^\\"]|\\.)*)"/) -> text

program: "\n"* (command "\n"+)* command?

textwithoutspaces: /(?:[^#\n،,， *+\-\/eiasbअ否אو]|א(?!חרת )|و(?!إلا )|否(?!则 )|अ(?!न्यथा )|b(?!aşka )|e(?!lse |llers )|s(?!inon |enão |ino )|i(?!naczej )|a(?!nders |lie |ltrimenti ))+/ -> text

_print_argument: (quoted_text | list_access | print_expression)* -> arg

print_expression: INT -> print_expression
  | NAME -> var
  | expression
  | /([-+]?(\d+[\.،,，]\d+))/ -> error_unsupported_number // error production

command_with_ifs: command -> command_no_ifs
  | NAME ("is" | "=") NAME "at" (INT | random) -> list_access_var

expression: simple_expression -> simple_expression
  | expression "*" atom -> multiplication
  | expression "/" atom -> division
  | expression "+" atom -> addition
  | expression "-" atom -> subtraction

simple_expression: atom "*" atom -> multiplication
  | atom "/" atom -> division
  | atom "+" atom -> addition
  | atom "-" atom -> subtraction

atom: INT -> int
  | "-" INT -> neg
  | NAME -> var
  | textwithoutspaces -> txt
  | /([-+]?(\d+[\.،,，]\d+))/ -> error_unsupported_number // error production

command: "print" _print_argument? -> print
  | NAME ("is" | "=") "as" _print_argument? -> ask
  | "forward" (NUMBER | list_access | textwithoutspaces)? -> forward
  | "turn" (NUMBER | list_access | textwithoutspaces)? -> turn
  | NAME ("is" | "=") (expression | textwithoutspaces | textwithspaces) -> assign
  | "sleep" (INT | list_access | NAME | expression)? -> sleep
  | "color" (color | list_access | textwithoutspaces)? -> color
  | "add" some_spaces "to" NAME -> add
  | "remove" some_spaces "form" NAME -> remove
  | NAME ("is" | "=") textwithspaces+ -> assign_list
  | "repeat" (INT | NAME) "times" command+ "\n" _END_BLOCK -> repeat
  | "if" condition "\n" command+ "\n" _END_BLOCK -> ifs
  | "if" condition "\n" command+ "\n" _END_BLOCK "\n" "else" command+ "\n" _END_BLOCK -> ifelse
  | text? -> error_invalid_space // error production
  | error_invalid // error production
  | "ask" text? -> error_ask_dep_2 // error production
  | "echo" text? -> error_echo_dep_2 // error production
  | error_invalid // error production
  | "repeat" (INT | NAME) "times" -> error_repeat_no_command // error production
  | "repeat" (INT | NAME) "times" _print_argument -> error_repeat_no_print // error production
  | "repeat" (INT | NAME) command_with_ifs -> error_repeat_no_times // error production

condition: (textwithoutspaces | NUMBER) ("is" | "=") (textwithoutspaces | NUMBER)+ -> equality_check
  | textwithoutspaces "in" var_access -> in_list_check
  | textwithoutspaces "is" textwithoutspaces (_SPACE textwithoutspaces)+ -> condition_spaces
